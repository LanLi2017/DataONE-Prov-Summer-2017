#  mq16: What data is immediately upstream of raw_image?

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3c.org/ns/prov#>
PREFIX p1:   <http://dataone.org/ns/provone#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?up_data_name
WHERE
{    
      ?entity        rdf:type             prov:Entity ;
                     rdfs:label           "raw_image".  
    
      ?entity        (^p1:hasDefaultParam/^p1:hasOutPort/p1:hasInPort/p1:hasDefaultParam)   ?upentity .
      
      ?upentity      rdf:type             prov:Entity ;
                     rdfs:label           ?up_data_name .
}
