#  mq7: What program blocks provide input directly to simulate_data_collection.collect_data_set?

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX p1:   <http://dataone.org/ns/provone#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?program_name
WHERE
{  
  ?upprogram     rdf:type            p1:Program ;
                 rdfs:label          ?program_name ;                 
                 p1:hasOutPort       ?outport .
    
  ?outport       rdf:type            p1:Port ;
                 rdfs:label          ?port_name .
      
  ?inport        rdf:type            p1:Port ;
                 rdfs:label          ?port_name .
                                     
  ?program       rdf:type            p1:Program ;
                 rdfs:label          "collect_data_set" ;
                 p1:hasInPort        ?inport .
    
  ?parentprogram rdf:type            p1:Workflow ;
                 rdfs:label          "simulate_data_collection" ;
                 p1:hasSubProgram    ?program .
}
