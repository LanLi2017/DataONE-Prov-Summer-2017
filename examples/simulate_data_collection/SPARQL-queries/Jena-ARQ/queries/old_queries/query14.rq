#Query 14: What program blocks are anywhere downstream of calculate_strategy?

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX p1:   <http://purl.dataone.org/provone/2015/01/15/ontology#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?down_block_name
WHERE 
 {    ?block         rdf:type             yw:Block ;
                     rdfs:label           "calculate_strategy" .                                        
    
      ?block       (yw:hasOutPort/yw:writes/^yw:reads/^yw:hasInPort)+   ?down_block .
      
      ?down_block      rdf:type           yw:Block ;
                       rdfs:label         ?down_block_name .
       
 } ORDER BY ?down_block_name
