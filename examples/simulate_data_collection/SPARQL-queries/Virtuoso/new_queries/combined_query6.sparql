 #Combined query 06: What are the downstream resources from "raw_image" data?

SPARQL BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX p1:   <http://dataone.org/ns/provone#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?resource_path
WHERE
{   #find the data that downstream to raw_image data: 
    ?data         rdf:type            yw:Data ;
                  rdfs:label          "raw_image".  
    
    ?data        (^yw:connectsTo/^yw:hasInPort/yw:hasOutPort/yw:connectsTo)+   ?down_data .
      
    ?down_data    rdf:type           yw:Data;
                  rdfs:label         ?down_data_name .
    
    #find the ports that attached to the downstream data
    ?port      rdf:type         yw:Port;
                rdfs:label       ?port_name;
                yw:connectsTo    ?down_data.

    #find the resource that attached to the ports
    ?resource   rdf:type     yw:Resource;
                yw:actualFilePath   ?resource_path;
                yw:isGeneratedBy    ?port.

};