#  mq13: What program blocks are upstream of transform_images?

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3c.org/ns/prov#>
PREFIX p1:   <http://dataone.org/ns/provone#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?up_program_name
WHERE 
{    
      ?program       rdf:type             p1:Program ;
                     rdfs:label           "transform_images" .   
                     
      ?program      (p1:hasInPort/p1:hasDefaultParam/^p1:hasDefaultParam/^p1:hasOutPort)+   ?up_program .
      
      ?up_program    rdf:type             p1:Program ;
                     rdfs:label           ?up_program_name .    
}
