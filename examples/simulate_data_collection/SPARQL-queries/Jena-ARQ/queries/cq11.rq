#  cq11: What are the downstream resources from "raw_image" data?

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX p1:   <http://purl.dataone.org/provone/2015/01/15/ontology#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?down_resource_path
WHERE
{     
    ?data         rdf:type            yw:DataNode ;
                  rdfs:label          "raw_image" ;  
                  (^yw:connectsTo/^yw:hasInPort)     ?down_block .
    
    ?down_block         rdf:type                yw:Block ;
                        (yw:hasOutPort/yw:connectsTo)      ?down_data .
    
    ?down_data          rdf:type                yw:DataNode ;
                        (^yw:connectsTo/^yw:hasInPort/yw:hasOutPort/yw:connectsTo)*     ?downstream_data .
    
    ?down_port          yw:connectsTo           ?downstream_data .
    
    ?resource     rdf:type            yw:Resource ;
                  yw:actualFilePath   ?down_resource_path ;
                  yw:isGeneratedBy    ?down_port .
}
ORDER BY ?down_resource_path