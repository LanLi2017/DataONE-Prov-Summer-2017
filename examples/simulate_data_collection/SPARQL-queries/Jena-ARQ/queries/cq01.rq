#  cq01: What samples did the run of the script collect images from (associated with "raw_image" data)?

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX p1:   <http://purl.dataone.org/provone/2015/01/15/ontology#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?sample_id
WHERE
{     
     ?data         rdf:type            yw:DataNode ;
                   rdfs:label          "raw_image" .
    
     ?port         yw:connectsTo       ?data .

     ?resource     rdf:type            yw:Resource ;
                   yw:isGeneratedBy    ?port ;
                   yw:hasURIVariable   ?URIVariable .

     ?URIVariable  rdf:type            yw:URIVariable ;
                   yw:variableName     "sample_id" ;
                   yw:variableValue    ?sample_id .
}
