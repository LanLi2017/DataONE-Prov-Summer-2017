#Query 17 (DownstreamDataName): What data is downstream of accepted_sample?

SPARQL BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3c.org/ns/prov#>
PREFIX p1:   <http://dataone.org/ns/provone#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?down_data_name
WHERE
{    
      ?data         rdf:type            yw:Data ;
                    rdfs:label           ?data_name.  
    
      ?data        (^yw:connectsTo/^yw:hasInPort/yw:hasOutPort/yw:connectsTo)+   ?down_data .
      
      ?down_data    rdf:type           yw:Data;
                    rdfs:label           ?down_data_name .

  FILTER (?data_name = "accepted_sample")
   
} ORDER BY ?down_data_name;