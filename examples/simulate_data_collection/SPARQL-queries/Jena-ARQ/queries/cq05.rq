#  cq05: What cassette (associated with "raw_image" data) held the sample from which "run/data/DRT240/DRT240_10000eV_010.img" was derived?

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX p1:   <http://purl.dataone.org/provone/2015/01/15/ontology#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?cassette_id
WHERE
{     
     ?data                   rdf:type            yw:DataNode ;
                             rdfs:label          "raw_image" .
    
     ?port                   yw:connectsTo       ?data .

     ?raw_resource           rdf:type            yw:Resource ;
                             yw:isGeneratedBy    ?port ;
                             yw:actualFilePath   ?raw_image_file ;
                             yw:hasURIVariable   ?raw_variable_1, ?raw_variable_2, ?raw_variable_3 , ?raw_variable_4 .
    
     ?raw_variable_1         rdf:type            yw:URIVariable ;
                             yw:variableName     ?variable_name_1 ;
                             yw:variableValue    ?variable_value_1 .

     ?raw_variable_2         rdf:type            yw:URIVariable ;
                             yw:variableName     ?variable_name_2 ;
                             yw:variableValue    ?variable_value_2 .

     ?raw_variable_3         rdf:type            yw:URIVariable ;
                             yw:variableName     ?variable_name_3 ;
                             yw:variableValue    ?variable_value_3 .

     ?raw_variable_4         rdf:type            yw:URIVariable ;
                             yw:variableName     "cassette_id" ;
                             yw:variableValue    ?cassette_id .
    
     ?corrected_resource     rdf:type            yw:Resource ;
                             yw:actualFilePath   "run/data/DRT240/DRT240_10000eV_010.img" ;
                             yw:hasURIVariable   ?corrected_variable_1, ?corrected_variable_2, ?corrected_variable_3 .

     ?corrected_variable_1   rdf:type            yw:URIVariable ;
                             yw:variableName     ?variable_name_1 ;
                             yw:variableValue    ?variable_value_1 .
    
     ?corrected_variable_2   rdf:type            yw:URIVariable ;
                             yw:variableName     ?variable_name_2 ;
                             yw:variableValue    ?variable_value_2 .
    
     ?corrected_variable_3   rdf:type            yw:URIVariable ;
                             yw:variableName     ?variable_name_3 ;
                             yw:variableValue    ?variable_value_3 .
    
    FILTER (?variable_name_1 != ?variable_name_2) 
    FILTER (?variable_name_1 != ?variable_name_3) 
    FILTER (?variable_name_2 != ?variable_name_3) 

}