#  mq17: What data is downstream of accepted_sample?

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3c.org/ns/prov#>
PREFIX p1:   <http://dataone.org/ns/provone#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?down_data_name
WHERE
{   
      ?entity        rdf:type             prov:Entity ;
                     rdfs:label           "accepted_sample" .  
    
      ?entity        (^p1:hasDefaultParam/^p1:hasInPort/p1:hasOutPort/p1:hasDefaultParam)+   ?downentity .
      
      ?downentity    rdf:type             prov:Entity ;
                     rdfs:label           ?down_data_name .
}
