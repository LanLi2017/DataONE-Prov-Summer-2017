#  mq22: What data are upstream 2 levels from corrected_image?

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX p1:   <http://dataone.org/ns/provone#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?up_data_name
WHERE
{   
      ?data           rdf:type           yw:Data ;
                      rdfs:label         ?data_name.  
    
      ?data           (^yw:connectsTo/^yw:hasOutPort/yw:hasInPort/yw:connectsTo/^yw:connectsTo/^yw:hasOutPort/yw:hasInPort/yw:connectsTo)   ?up_data .
      
      ?up_data         rdf:type             yw:Data ;
                       rdfs:label           ?up_data_name .

FILTER (?data_name = "corrected_image")
}