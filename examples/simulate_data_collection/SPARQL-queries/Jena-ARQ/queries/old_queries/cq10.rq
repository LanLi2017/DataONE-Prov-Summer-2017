#  cq10: What are the program blocks between "run/rejected_samples.txt" and "cassette_q55_spreadsheet.csv"? List the program blocks in downstream order between the two files.

BASE         <http://yesworkflow.org/0000000000/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX p1:   <http://purl.dataone.org/provone/2015/01/15/ontology#>
PREFIX yw:   <http://yesworkflow.org/ns/yesworkflow>

SELECT DISTINCT ?down_block_name
WHERE
{     
    ?resource1          rdf:type                    yw:Resource ;
                        yw:actualFilePath           "run/rejected_samples.txt" ;
                        yw:isGeneratedBy            ?port1 .  

    ?resource2          rdf:type                    yw:Resource ;
                        yw:actualFilePath           "cassette_q55_spreadsheet.csv" ; 
                        yw:isGeneratedBy            ?port2 .
    
    ?port1              yw:connectsTo               ?data1 .
    
    ?port2              yw:connectsTo               ?data2 .
    
    {   
        ?inport             yw:connectsTo               ?data1 .              

        ?outport            yw:connectsTo               ?data2 .

        ?block1             rdf:type                    yw:Block ;
                            yw:hasInPort                ?inport .

        ?block2             rdf:type                    yw:Block ; 
                            yw:hasOutPort               ?outport .

        ?block1             ((yw:hasOutPort/yw:connectsTo/^yw:connectsTo/^yw:hasInPort)*)      ?down_block .

        ?down_block         ((yw:hasOutPort/yw:connectsTo/^yw:connectsTo/^yw:hasInPort)*)      ?block2.     
    }
    UNION
    {
        ?inport             yw:connectsTo               ?data2 .              

        ?outport            yw:connectsTo               ?data1 .

        ?block1             rdf:type                    yw:Block ;
                            yw:hasInPort                ?inport .

        ?block2             rdf:type                    yw:Block ; 
                            yw:hasOutPort               ?outport .

        ?block1             ((yw:hasOutPort/yw:connectsTo/^yw:connectsTo/^yw:hasInPort)*)      ?down_block .

        ?down_block         ((yw:hasOutPort/yw:connectsTo/^yw:connectsTo/^yw:hasInPort)*)      ?block2.
    }
    
    ?down_block         rdf:type                    yw:Block ;
                        rdfs:label                  ?down_block_name .
}
